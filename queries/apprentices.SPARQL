PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX xl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gvp: <http://vocab.getty.edu/ontology#>
PREFIX aat: <http://vocab.getty.edu/aat/>
PREFIX rel: <http://ldf.fi/schema/relations/>
CONSTRUCT {
   [] a rel:Relation_directed_p2p ;
    rel:relationSubject ?person ;
    rel:relationObject ?person2 ;
    rdfs:label ?description ;
    rel:relationType rel:apprenticeOf .
}
WHERE {
  ?person a gvp:PersonConcept .
  ?person xl:prefLabel ?label .
  ?label gvp:term ?name .
  FILTER (LANG(?name) = '')
  ?person2 a gvp:PersonConcept .
  ?person2 xl:prefLabel ?label2 .
  ?label2 gvp:term ?name2 .
  FILTER (LANG(?name2) = '')
  ?person gvp:ulan1105_apprentice_of ?person2 .
  BIND (CONCAT(?name, " was an apprentice of ", ?name2, ".") AS ?description)
  
}