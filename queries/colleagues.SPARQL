PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX xl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gvp: <http://vocab.getty.edu/ontology#>
PREFIX aat: <http://vocab.getty.edu/aat/>
PREFIX rel: <http://ldf.fi/schema/relations/>
PREFIX rlt: <http://ldf.fi/relations/types/>

CONSTRUCT {
  rlt:colleagueOf a rel:RelationType ;
      rdfs:label ?label . 
  
  rlt:colleagues a rel:RelationType ;
      rdfs:label ?label2 . 
  
  [] a rel:Relation_directed_p2p ;
    rel:relationSubject ?newURI ;
    rel:relationObject ?newURI2 ;
    rdfs:label ?description ;
    rel:relationType rlt:colleagueOf .
  
  [] a rel:Relation_directed_p2p ;
    rel:relationSubject ?newURI2 ;
    rel:relationObject ?newURI ;
    rdfs:label ?invDescription ;
    rel:relationType rlt:colleagueOf .

  [] a rel:Relation_undirected ;
    rel:connectedEntity ?newURI ;
    rel:connectedEntity ?newURI2 ;
    rdfs:label ?description2 ;
    rel:relationType rlt:colleagues .
}
WHERE {
  ?personConcept a gvp:PersonConcept;
      gvp:prefLabelGVP/gvp:term ?name;
      foaf:focus ?person.
  ?personConcept2 a gvp:PersonConcept;
	    gvp:prefLabelGVP/gvp:term ?name2;
  	  foaf:focus ?person2.

  ?personConcept gvp:ulan1301_colleague_of ?personConcept2 .

  BIND(STRAFTER(str(?personConcept), "/ulan/") AS ?id)
  BIND(STRAFTER(str(?personConcept2), "/ulan/") AS ?id2)
  BIND(URI(CONCAT("http://ldf.fi/ulan/", ?id)) AS ?newURI)
  BIND(URI(CONCAT("http://ldf.fi/ulan/", ?id2)) AS ?newURI2)
  
  BIND("was colleague of" AS ?label)
  BIND("were colleagues" AS ?label2)
  BIND (CONCAT(?name, " ", ?label, " ", ?name2, ".") AS ?description)
  BIND (CONCAT(?name2, " ", ?label, " ", ?name, ".") AS ?invDescription)
  BIND (CONCAT(?name, " and ", ?name2, " ", ?label2) AS ?description2)
  BIND( IF(STR(?personConcept) > STR(?personConcept2), ?person, ?person2) AS ?result)
  
  FILTER(?result != ?person2)
}