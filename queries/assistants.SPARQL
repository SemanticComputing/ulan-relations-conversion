PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX xl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gvp: <http://vocab.getty.edu/ontology#>
PREFIX aat: <http://vocab.getty.edu/aat/>
PREFIX rel: <http://ldf.fi/schema/relations/>
PREFIX rlt: <http://ldf.fi/relations/types/>

CONSTRUCT {
  rlt:assistantOf a rel:RelationType ;
      rdfs:label ?label. 
  
  rlt:assistants a rel:RelationType ;
      rdfs:label ?label2. 


   [] a rel:Relation_directed_p2p ;
    rel:relationSubject ?person ;
    rel:relationObject ?person2 ;
    rdfs:label ?description ;
    rel:relationType rlt:assistantOf.

    [] a rel:Relation_undirected ;
    rel:connectedEntity ?person ;
    rel:connectedEntity ?person2 ;
    rdfs:label ?description2 ;
    rel:relationType rlt:assistants.
}
WHERE {
  ?personConcept a gvp:PersonConcept;
     gvp:prefLabelGVP/gvp:term ?name;
    foaf:focus ?person.

  ?personConcept2 a gvp:PersonConcept;
	 gvp:prefLabelGVP/gvp:term ?name2;
  	foaf:focus ?person2.
  
  ?personConcept gvp:ulan1307_assistant_of ?personConcept2 .

  BIND("was assistant of" AS ?label)
  BIND("had an assistant-superior relationship" AS ?label2)
  BIND (CONCAT(?name, " ", ?label, " ", ?name2, ".") AS ?description)
  BIND (CONCAT(?name, " and ", ?name2, " ", ?label2) AS ?description2)
}