PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX xl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gvp: <http://vocab.getty.edu/ontology#>
PREFIX aat: <http://vocab.getty.edu/aat/>
PREFIX rel: <http://ldf.fi/schema/relations/>
PREFIX rlt: <http://ldf.fi/relations/types/>

CONSTRUCT {
  rlt:nephewNieceOf a rel:RelationType ;
      rdfs:label ?nLabel. 
  
  rlt:uncleAuntOf a rel:RelationType ;
      rdfs:label ?uLabel. 
  
  rlt:nephewsNieces a rel:RelationType ;
      rdfs:label ?label.     

  [] a rel:Relation_directed_p2p ;
    rel:relationSubject ?newURI ;
    rel:relationObject ?newURI2 ;
    rdfs:label ?nDescription ;
    rel:relationType rlt:nephewNieceOf .
  
  [] a rel:Relation_directed_p2p ;
    rel:relationSubject ?newURI2 ;
    rel:relationObject ?newURI ;
    rdfs:label ?uDescription ;
    rel:relationType rlt:uncleAuntOf .
  
  [] a rel:Relation_undirected ;
    rel:connectedEntity ?newURI ;
    rel:connectedEntity ?newURI2 ;
    rdfs:label ?description ;
    rel:relationType rlt:nephewsNieces  .
}
WHERE {
  
  ?personConcept gvp:ulan1531_nephew-niece_of ?personConcept2 .
  
  ?personConcept a gvp:PersonConcept;
    gvp:prefLabelGVP/gvp:term ?name;
    foaf:focus ?person.

  ?personConcept2 a gvp:PersonConcept;
	  gvp:prefLabelGVP/gvp:term ?name2;
  	foaf:focus ?person2.

  BIND(STRAFTER(str(?personConcept), "/ulan/") AS ?id)
  BIND(STRAFTER(str(?personConcept2), "/ulan/") AS ?id2)
  BIND(URI(CONCAT("http://ldf.fi/ulan/", ?id)) AS ?newURI)
  BIND(URI(CONCAT("http://ldf.fi/ulan/", ?id2)) AS ?newURI2)

  BIND("was nephew/niece of" AS ?nLabel)
  BIND("was uncle/aunt of" AS ?uLabel)
  BIND("had a nephew/niece - uncle/aunt relationship" AS ?label)
  BIND (CONCAT(?name, " ", ?nLabel, " ", ?name2, ".") AS ?nDescription)
  BIND (CONCAT(?name2, " ", ?uLabel, " ", ?name, ".") AS ?uDescription)
  BIND (CONCAT(?name, " and ", ?name2, " ", ?label) AS ?description)
}