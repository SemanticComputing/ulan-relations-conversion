PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX xl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gvp: <http://vocab.getty.edu/ontology#>
PREFIX aat: <http://vocab.getty.edu/aat/>
PREFIX rel: <http://ldf.fi/schema/relations/>
PREFIX rlt: <http://ldf.fi/relations/types/>

CONSTRUCT {
  rlt:grandchildOf a rel:RelationType ;
      rdfs:label ?cLabel . 
  
  rlt:grandparentOf a rel:RelationType ;
      rdfs:label ?pLabel . 
  
  rlt:grandchildren a rel:RelationType ;
      rdfs:label ?label .     

  [] a rel:Relation_directed_p2p ;
    rel:relationSubject ?newURI ;
    rel:relationObject ?newURI2 ;
    rdfs:label ?cDescription ;
    rel:relationType rlt:grandchildOf .
  
  [] a rel:Relation_directed_p2p ;
    rel:relationSubject ?newURI ;
    rel:relationObject ?newURI2 ;
    rdfs:label ?pDescription ;
    rel:relationType rlt:grandparentOf .
  
  [] a rel:Relation_undirected ;
    rel:connectedEntity ?newURI ;
    rel:connectedEntity ?newURI2 ;
    rdfs:label ?description ;
    rel:relationType rlt:grandchildren .
}
WHERE {
  ?personConcept a gvp:PersonConcept;
    gvp:prefLabelGVP/gvp:term ?name;
    foaf:focus ?person.
  
  ?personConcept2 a gvp:PersonConcept;
	  gvp:prefLabelGVP/gvp:term ?name2;
  	foaf:focus ?person2.
  
  ?personConcept gvp:ulan1513_grandchild_of ?personConcept2 .
  
  BIND(STRAFTER(str(?personConcept), "/ulan/") AS ?id)
  BIND(STRAFTER(str(?personConcept2), "/ulan/") AS ?id2)
  BIND(URI(CONCAT("http://ldf.fi/ulan/", ?id)) AS ?newURI)
  BIND(URI(CONCAT("http://ldf.fi/ulan/", ?id2)) AS ?newURI2)

  BIND("was grandchild of" AS ?cLabel)
  BIND("was grandparent of" AS ?pLabel)
  BIND("had a grandchild-grandparent relationship" AS ?label)
  BIND (CONCAT(?name, " ", ?cLabel, " ", ?name2, ".") AS ?cDescription)
  BIND (CONCAT(?name2, " ", ?pLabel, " ", ?name, ".") AS ?pDescription)
  BIND (CONCAT(?name, " and ", ?name2, " ", ?label) AS ?description)
  BIND( IF(STR(?personConcept) > STR(?personConcept2), ?person, ?person2) AS ?result)
  
  FILTER(?result != ?person2)
  
}